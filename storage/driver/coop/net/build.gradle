description = "mongoose-storage-driver-coop-net"

dependencies {

	compileOnly(
		project(":"),
		project(":storage:driver:coop"),
		"com.github.akurilov:confuse:${depVersion.confuse}",
		"com.github.akurilov:confuse-io-json:${depVersion.confuseIoJson}",
		"com.github.akurilov:fiber4j:${depVersion.fiber4j}",
		"org.apache.logging.log4j:log4j-api:${depVersion.log4j}",
		"org.apache.logging.log4j:log4j-core:${depVersion.log4j}",
	)

	provided(
		"com.github.akurilov:netty-connection-pool:${depVersion.nettyConnectionPool}",
		"io.netty:netty-buffer:${depVersion.netty}",
		"io.netty:netty-common:${depVersion.netty}",
		"io.netty:netty-handler:${depVersion.netty}",
		"io.netty:netty-transport:${depVersion.netty}",
		// the deps below are the runtime deps actually
		"io.netty:netty-codec:${depVersion.netty}",
		"io.netty:netty-resolver:${depVersion.netty}",
		"io.netty:netty-transport-native-epoll:${depVersion.netty}:linux-x86_64",
		"io.netty:netty-transport-native-kqueue:${depVersion.netty}:osx-x86_64",
		"io.netty:netty-transport-native-unix-common:${depVersion.netty}",
		"org.javassist:javassist:${depVersion.javassist}",
	)
}

jar {

	baseName = project.description
	duplicatesStrategy = DuplicatesStrategy.EXCLUDE

	manifest {
		attributes (
			"Implementation-Version": version,
			"Implementation-Title": project.description,
		)
	}

	// 1st level dependencies only
	from configurations
		.provided
		.copyRecursive()
		.setTransitive(false)
		.filter {
			// do not include project dependencies (include module dependencies only)
			!it.absolutePath.startsWith(rootProject.projectDir.absolutePath)
		}
		.collect {
			zipTree it
		}

	exclude "META-INF/*.RSA", "META-INF/*.SF", "META-INF/*.DSA"
}
